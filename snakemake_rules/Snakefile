configfile: "/home/mei.wu/rna-seq/config/config.yaml"

COMMON_DIR = config["output_dir"]
TMP_DIR = config["tmp_dir"]

rule end:
    input:
        expand(COMMON_DIR + "/{sample}/trim-galore/{sample}_{num}_val_{num}.fq.gz", sample=config["read1"], num=["1","2"]),
        COMMON_DIR + "/star_index/",
        expand(COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.toTranscriptomeSorted.out.bam", sample=config["read1"]),
        expand(COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.{f}.out.bam.bai", sample=config["read1"], f=["sortedByCoord","toTranscriptomeSorted"]),
        expand(COMMON_DIR + "/{sample}/picard_markdupe/{sample}.bam", sample=config["read1"]),
        expand(COMMON_DIR + "/{sample}/multiqc/report.html", sample=config["read1"]),

rule fastqc:
    input:
        read1 = lambda wildcards: config["read1"][wildcards.sample],
        read2 = lambda wildcards: config["read2"][wildcards.sample]
    output:
        qc_read1 = COMMON_DIR + "/{sample}/fastqc/{sample}_1_fastqc.zip",
        qc_read2 = COMMON_DIR + "/{sample}/fastqc/{sample}_2_fastqc.zip"
    params:
        tmp_dir = TMP_DIR,
        outpath = COMMON_DIR + "/{sample}/fastqc/",
        job_name = "fastqc_{sample}_MW"
    shell:
        "fastqc --noextract --dir {params.tmp_dir} --outdir {params.outpath} {input.read1};"
        "fastqc --noextract --dir {params.tmp_dir} --outdir {params.outpath} {input.read2};"

rule trim_galore_pe:
    input:
        read1 = lambda wildcards: config["read1"][wildcards.sample],
        read2 = lambda wildcards: config["read2"][wildcards.sample]
    output:
        trimmed_1 = COMMON_DIR + "/{sample}/trim-galore/{sample}_1_val_1.fq.gz",
        trimmed_2 = COMMON_DIR + "/{sample}/trim-galore/{sample}_2_val_2.fq.gz"
    params:
        outpath = COMMON_DIR + "/{sample}/trim-galore/",
        job_name = "trimgalore_{sample}_MW",
    resources:
        cores=8,
        mem_mb=5000,
    shell:
         "trim_galore --cores {resources.cores} --fastqc --gzip --output_dir {params.outpath} --paired {input.read1} {input.read2}"

# rule star_index:
#     input:
#          fa = config["genome"],
#          gtf = config["gtf"]
#     output:
#          directory(COMMON_DIR + "/star_index/")
#     threads: 36
#     shell:
#          'STAR --runThreadN {threads} '
#          '--runMode genomeGenerate '
#          '--genomeDir {output} '
#          '--genomeFastaFiles {input.fa} '
#          '--sjdbGTFfile {input.gtf} '
#          '--sjdbOverhang 99'

rule star_align:
    input:
        trimmed_1 = COMMON_DIR + "/{sample}/trim-galore/{sample}_1_val_1.fq.gz",
        trimmed_2 = COMMON_DIR + "/{sample}/trim-galore/{sample}_2_val_2.fq.gz",
        ref_dir = COMMON_DIR + "/star_index/"
    output:
        COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.sortedByCoord.out.bam",
        COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.toTranscriptome.out.bam",
        COMMON_DIR + "/{sample}/star_aln/{sample}.Log.final.out"
    params:
        tmp_dir = TMP_DIR,
        prefix = COMMON_DIR + "/{sample}/star_aln/{sample}.",
        job_name = "star_{sample}_MW"
    log:
        COMMON_DIR + "/{sample}/star_aln/logs/{sample}-star.log",
    resources:
        cores=8,
        mem_mb=20000,
        time_min=600,
    shell:
        "STAR --genomeDir {input.ref_dir} "
        "--readFilesIn {input.trimmed_1} {input.trimmed_2} "
        "--readFilesCommand zcat "        
        "--twopassMode Basic "
        "--quantMode TranscriptomeSAM " # using RSEM
        "--peOverlapNbasesMin 10 "
        "--outSAMtype BAM SortedByCoordinate "
        "--outFileNamePrefix {params.prefix} "
        "--outTmpDir {params.tmp_dir} "
        "--runThreadN {resources.cores}"


# rule rename_bam: # I will add this rule .. later to comply w/ conventions ^_^

rule samtools_sort:
    input:
        transcript = COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.toTranscriptome.out.bam"
    output:
        COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.toTranscriptomeSorted.out.bam",
    params:
        tmp_dir = TMP_DIR,
        job_name = "samsort_{sample}_MW",
    resources:
        cores=8,
        mem_mb=20000,
        time_min=480,
    shell:
        "samtools sort -T {params.tmp_dir} "
        "--threads {resources.cores} "
        "-O bam {input.transcript} > {output}"

rule samtools_index:
    input:
        coord = COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.sortedByCoord.out.bam",
        transcript = COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.toTranscriptomeSorted.out.bam"
    output:
        coord_bai = COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.sortedByCoord.out.bam.bai",
        transcript_bai = COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.toTranscriptomeSorted.out.bam.bai"
    params:
        job_name = "samindex_{sample}_MW",
    resources:
        cores=5,
    shell:
        "samtools index -b -@ {resources.cores} {input.coord};"
        "samtools index -b -@ {resources.cores} {input.transcript};"

rule mark_dupes:
    input:
        COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.sortedByCoord.out.bam"
    output:
        bam=COMMON_DIR + "/{sample}/picard_markdupe/{sample}.bam",
        metrics=COMMON_DIR + "/{sample}/picard_markdupe/{sample}.metrics.txt"
    log:
        COMMON_DIR + "/{sample}/logs/{sample}-mdupe.log"
    params:
        "REMOVE_DUPLICATES=true",
        "tmp_dir=" + TMP_DIR
    wrapper:
        "0.68.0/bio/picard/markduplicates"

rule rsem:
    input:
        star_aln=COMMON_DIR + "/{sample}/star_aln/{sample}.Aligned.toTranscriptome.out.bam",
        ref_dir=COMMON_DIR + "/star_index/"
    output:
        COMMON_DIR + "/{sample}/rsem/{sample}.stat/{sample}.cnt",
    params:
        tmp_dir = TMP_DIR,
        prefix = COMMON_DIR + "/{sample}/rsem/{sample}",
        job_name = "rsem_{sample}_MW"
    resources:
        cores=8,
        mem_mb=20000,
        time_min=480,
    shell:
        "rsem-calculate-expression "
        "--star "
        "--num-threads {resources.cores} "
        "--temporary-folder {params.tmp_dir} "
        "--alignments {input.star_aln} "
        "{input.ref_dir} " # path to the genome index generated by STAR
        "{params.prefix}"

rule summary:
    input:
        qc_read1 = COMMON_DIR + "/{sample}/fastqc/{sample}_1_fastqc.zip",
        qc_read2 = COMMON_DIR + "/{sample}/fastqc/{sample}_2_fastqc.zip",
        star_aln = COMMON_DIR + "/{sample}/star_aln/{sample}.Log.final.out",
        metrics=COMMON_DIR + "/{sample}/picard_markdupe/{sample}.metrics.txt",
        rsem=COMMON_DIR + "/{sample}/rsem/{sample}.stat/{sample}.cnt",
    output:
        COMMON_DIR + "/{sample}/multiqc/report.html"
    params:
        inpath = COMMON_DIR + "/{sample}/"
    shell:
         "multiqc {params.inpath} --filename {output}"
